#!/bin/csh
# Written by Furey.
# With additions from Tony and Alander.
# Modified for Legacy by Justin Rocha (Xenith) 2002-02-03


# Set the port number.
set port = 3000
if ( "$1" != "" ) set port="$1"

# Change to area directory.
cd area
../bin/sand_server &
# Set limits.
if ( -e ../misc/shutdown.txt ) rm -f ../misc/shutdown.txt

while ( 1 )
    # If you want to have logs in a different directory,
    # change the 'set logfile' line to reflect the directory name.
    if ( $port == 3000 ) then
        set index = 1000
        while ( 1 )
            set logfile = ../log/$index.log
            if ( ! -e $logfile ) break
            @ index++
        end
    endif

    # Run Legacy.
    ../bin/legacy $port >&! $logfile


    # Restart, giving old connections a chance to die.
    if ( -e ../misc/shutdown.txt ) then
	    rm -f ../misc/shutdown.txt
	    pkill sand_server
	    exit 0
    endif
    sleep 15
end
